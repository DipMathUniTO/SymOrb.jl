"""
    ϕ(g::GroupElement)::Matrix

Compute the matrix representation of the action of a group element `g` ∈ G on the configuration space
"""
function ϕ(g::GroupElement)::Matrix
    M = zeros(  size(g.M, 1), size(g.σ, 1), size(g.M, 1), size(g.σ, 1))

    for (ix, v) in enumerate(g.σ)
        M[:, ix, :, v] .= g.M
    end

    return reshape(M, size(M, 1) * size(M, 2), size(M, 1) * size(M, 2))
end

"""
     ϕg_n(gs::Vector{GroupElement})::Vector{Matrix} 

Compute the matrices that represent the action of the group elements `gs` ⊂ G on the configuration space
"""
function ϕg_n(gs::Vector{GroupElement})::Vector{Matrix} 
    if (length(gs) > 1)
        ϕ.(gs)
    else
        [I(size(gs[1].M, 1) * size(gs[1].σ, 1))]
    end
end


"""
    π_H(H::Vector{GroupElement})::Matrix 
    π_H(H::GroupElement)::Matrix

Compute the matrix representation of the projection onto the subspace generated by the group elements `Hs`⊂ G or by a single group element `H` ∈ G
"""
π_H(Hs::Vector{GroupElement})::Matrix = sum(ϕ.(Hs)) / length(Hs)

π_H(H::GroupElement)::Matrix = (I(size(H.M,1) * size(H.σ, 1)) + ϕ(H))/2


"""
   π_bc(G::SymmetryGroup, dims)::Matrix{Float64} 

Compute the matrix representation of the projection onto the initial and final boundary conditions.
"""
function π_bc(G::SymmetryGroup, dims)::Matrix{Float64} 
    F, N, dim = dims
    M = zeros(N*dim,  F+2, N*dim, F+2)

    if (G.action_type == Cyclic)
        M[:,  1 , :,  1 ] = I(N*dim)/2
        M[:,  1 , :, F+2] = ϕg_n(G.g)[end - 1] / 2
        M[:, F+2, :,  1 ] = ϕg_n(G.g)[1] / 2
        M[:, F+2, :, F+2] = I(N*dim)/2
    else
        M[:,  1 , :,  1 ] = π_H(G.H0)
        M[:, F+2, :, F+2] = π_H(G.H1)
    end

    for i ∈ 2:F+1
        M[:, i, :, i] = I(N*dim)
    end

    reshape(M, (F+2)*N*dim, (F+2)*N*dim)
end

"""
    π_kerT(G::SymmetryGroup, dims)::Matrix{Float64}

Compute the matrix representation of the projection onto the kernel of the action of `τ` on `O(2)`
"""
function π_kerT(G::SymmetryGroup, dims)::Matrix
    F, N, dim = dims
    M = zeros(N*dim, F+2, N*dim, F+2)

    for i ∈ 1:F+2
        M[:, i, :, i] = π_H(G.kerT)
    end

    reshape(M, (F+2)*N*dim, (F+2)*N*dim)
end

"""
    nth_body(m::Vector{Float64}, dims)::Matrix{Float64}

Compute the matrix representation of operator that reconstructs the nth body giben the masses `m` and the dimensions `dims`
"""
function nth_body(m::Vector{T}, dims)::Matrix{T} where T
    F, N, dim = dims
    M = zeros(dim, N, F+2, dim, N-1, F+2)
    for k ∈ axes(M, 3), i ∈ axes(M, 5)
        M[:,  i , k, :, i, k] = I(dim)
        M[:, end, k, :, i, k] = - m[i] / m[end] * I(dim)
    end
    return reshape(M, (F+2)*N*dim, (F+2)*(N-1)*dim)
end

"""
    remove_nth_body(dims)::Matrix{Float64}

Compute the matrix representation of the operator that removes the nth body given the dimensions `dims`
"""
function remove_nth_body(dims)
    F, N, dim = dims
    M = zeros(dim, N-1, F+2, dim, N, F+2)
    for i in axes(M, 2), k in axes(M, 3)
        M[:, i, k, :, i, k] = I(dim)
    end
    return reshape(M, (F+2)*(N-1)*dim, (F+2)*N*dim)
end

""" 
    project(G::SymmetryGroup, dims::NTuple{3, Int64})::Matrix

Compute the matrix representation of the projection operator onto the subspace of the configuration space that is invariant under the action of the symmetry group `G`
"""
project(G::SymmetryGroup, dims::NTuple{3, Int64})::Matrix = π_kerT(G, dims) * π_bc(G, dims)